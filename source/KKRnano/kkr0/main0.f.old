      PROGRAM MAIN0
C
C Explanation of most variables follows below
C
C     ALAT                     : lattice constant (in a.u.)
C     ABASIS,BBASIS,CBASIS,    : scaling factors for rbasis
C     E1,E2,                   : energies needed in EMESHT
C     HFIELD                   : external magnetic field, for
C                              : initial potential shift in
C                              : spin polarised case
C     TK,                      : temperature
C     VCONST,                  : potential shift
C     BRAVAIS(3,3),            : bravais lattice vectors
C     RECBV(3,3),              : reciprocal basis vectors
C     CLEB(NCLEB,2),           : GAUNT coefficients (GAUNT)
C     RMTREF(NREFD),           : muffin-tin radius of reference system
C     RBASIS(3,NAEZD),         : position of atoms in the unit cell
C                              : in units of bravais vectors
C     RCLS(3,NACLSD,NCLSD),    : real space position of atom in cluster
C     RR(3,0:NRD)              : set of real space vectors (in a.u.)
C     VBC(2),                  : potential constants
C     WG(LASSLD),              : integr. weights for Legendre polynomials
C     YRG(LASSLD,0:LASSLD)     : spherical harmonics (GAUNT2)
C     ZAT(NAEZD)               : nuclear charge
C     INTERVX,INTERVY,INTERVZ, : number of intervals in x,y,z-direction
C                              : for k-net in IB of the BZ
C     ICST,                    : number of Born approximation
C     IEND,                    : number of nonzero gaunt coeffizients
C     IFILE,                   : unit specifier for potential card
C     IPE,IPF,IPFE,            : not real used, IPFE should be 0
C     KHFELD,                  : 0,1: no / yes external magnetic field
C     KVREL,                   : 0,1 : non / scalar relat. calculation
C     LMAX,                    : maximum l component in
C                              : wave function expansion
C     LPOT,                    : maximum l component in
C                              : potential expansion
C     NAEZ,                    : number of atoms in unit cell
C     NCLS,                    : number of reference clusters
C     NPNT1,NPNT2,NPNT3,       : number of E points (EMESHT)
C     NPOL,                    : number of Matsubara Pols (EMESHT)
C     NR,                      : number of real space vectors rr
C     NREF,                    : number of diff. ref. potentials
C     NSPIN,                   : counter for spin directions
C     IGUESS                   : 0,1 : no / yes (sc) initial guess, set
C                              : IGUESSD to 1 in inc.p if needed
C     BCP                      : 0,1 : no / yes bc-preconditioning, set
C                              : BCPD to 1 in inc.p if needed
C     QBOUND                   : exit condition for self-consistent 
C                              : iteration 
C     QMRBOUND                 : exit condition for QMR iterations
C     SCFSTEPS                 : number of scf iterations
C     INIPOL(NAEZD),           : initial spin polarisation
C
C     ATOM(NACLSD,NAEZD),      : atom at site in cluster
C     CLS(NAEZD),              : cluster around atom
C     NACLS(NCLSD),            : number of atoms in cluster
C     EZOA(NACLSD,NAEZD),      : EZ of atom at site in cluster
C     ICLEB(NCLEB,3),          : pointer array
C     RMT(NAEZD)               : muffin-tin radius of true system
C     RMTNEW(NAEZD)            : adapted muffin-tin radius 
C     RWS(NAEZD)               : Wigner Seitz radius
C     ICST                     : the regular non spherical wavefunctions, the
C                              : alpha matrix and the t-matrix in the ICST-th. born approx
C     IMT(NAEZD),              : r point at MT radius
C     IPAN(NAEZD),             : number of panels in non-MT-region
C     IRC(NAEZD),              : r point for potential cutting
C     IRCUT(0:IPAND,NAEZD),    : r points of panel borders
C     IRMIN(NAEZD),            : max r for spherical treatment
C     IRNS(NAEZD)              : number r points for non spher. treatm.
C     IRWS(NAEZD),             : r point at WS radius
C     LMSP(NAEZD,LMXSPD)       : 0,1 : non/-vanishing lm=(l,m) component
C                              : of non-spherical potential
C     LLMSP(NAEZD,NFUND)       : lm=(l,m) of 'nfund'th nonvanishing
C                              : component of non-spherical pot.
C     JEND(LMPOTD,             : pointer array for icleb()
C     LOFLM(LM2D),             : l of lm=(l,m) (GAUNT)
C     NTCELL(NAEZD),           : index for WS cell
C     REFPOT(NAEZD)            : ref. pot. card  at position
C
C     A(NAEZD),B(NAEZD)        : contants for exponential r mesh
C     R(IRMD,NAEZD)            : radial mesh ( in units a Bohr)
C     DRDI(IRMD,NAEZD)         : derivative dr/di
C     THETAS(IRID,NFUND,NCELLD): shape function
C                              :         ( 0 outer space
C                              : THETA = (
C                              :         ( 1 inside WS cell
C                              : in spherical harmonics expansion
C     LCORE(20,NPOTD)          : angular momentum of core states
C     NCORE(NPOTD)             : number of core states
C ----------------------------------------------------------------------

C     the inc.p wrapper module is used to include the inc.p and inc.cls
C     parameters - this works also for free form source files
      USE inc_p_wrapper_module
      IMPLICIT NONE
C
C      INCLUDE 'inc.p'
C      INCLUDE 'inc.cls'
C     .. Parameters ..
      INTEGER   LASSLD,LMMAXD,LMPOTD,LMXSPD,LM2D,
     &          MAXMSHD,NPOTD,NSYMAXD
      PARAMETER (LMMAXD= (LMAXD+1)**2)
      PARAMETER (NPOTD= NSPIND*NAEZD)
      PARAMETER (LMPOTD= (LPOTD+1)**2)
      PARAMETER (LMXSPD= (2*LPOTD+1)**2)
      PARAMETER (LASSLD=4*LMAXD)
      PARAMETER (LM2D= (2*LMAXD+1)**2)
C     Maximal number of Brillouin zone symmetries, 48 is largest
C     possible number
      PARAMETER (NSYMAXD=48)
C     Maximal number of k-meshes used
      PARAMETER (MAXMSHD=8)

C     inc.p, inc.cls parameters
C     LMAXD
C     NAEZD
C     LPOTD
C     LMAXD
C     NREFD
C     IRID
C     BCPD
C     NACLSD
C     NCLEB
C     IRMD
C     IEXMD
C     NGSHD
C     IGUESSD
C     IPAND
C     ISHLD
C     IRNSD
C     KPOIBZ
C     KREL
C     NFUND
C     NATRCD
C     NCLSD
C     NMAXD
C     NRD
C     NSPIND
C     NUTRCD
C     NXIJD


C     ..
C     .. Local Scalars ..

      DOUBLE PRECISION RMAX
      DOUBLE PRECISION GMAX

      DOUBLE PRECISION RCUTJIJ
      DOUBLE PRECISION RCUTTRC
      DOUBLE PRECISION VCONST

      INTEGER ICST
C     eq IRMD ?
      INTEGER IRM
      INTEGER ISHIFT
      INTEGER KHFELD
      INTEGER KPRE
      INTEGER KTE
      INTEGER KVMAD
      INTEGER KVREL
      INTEGER KXC
      INTEGER LMAX
      INTEGER LMPOT
      INTEGER LPOT
      INTEGER MAXMESH

      INTEGER NREF
      INTEGER NSPIN
      INTEGER NSRA

      INTEGER KFORCE

      LOGICAL JIJ
      LOGICAL LDAU
C     ..
C     .. Local Arrays ..
      DOUBLE PRECISION VBC(2)

C     .. Energy Mesh ..
      DOUBLE PRECISION E1
      DOUBLE PRECISION E2
      DOUBLE PRECISION E2IN
      DOUBLE PRECISION EFERMI
      DOUBLE PRECISION TK

      INTEGER NPNT1
      INTEGER NPNT2
      INTEGER NPNT3
      INTEGER NPOL
      INTEGER IE
      INTEGER IELAST

      DOUBLE COMPLEX EZ(IEMXD)
      DOUBLE COMPLEX WEZ(IEMXD)
      INTEGER KMESH(IEMXD)

C     .. Lattice ..
      INTEGER NAEZ
      INTEGER NR

      DOUBLE PRECISION ALAT
      DOUBLE PRECISION VOLUME0

      DOUBLE PRECISION BRAVAIS(3,3)
      DOUBLE PRECISION RECBV(3,3)

      DOUBLE PRECISION RBASIS(3,NAEZD)
      DOUBLE PRECISION RMT(NAEZD)
      DOUBLE PRECISION RMTREF(NREFD)
      DOUBLE PRECISION RR(3,0:NRD)
      DOUBLE PRECISION RWS(NAEZD)
      DOUBLE PRECISION ZAT(NAEZD)

C     .. Lattice aux. ..
      DOUBLE PRECISION ABASIS
      DOUBLE PRECISION BBASIS
      DOUBLE PRECISION CBASIS


C     .. shape functions ..
      DOUBLE PRECISION THETAS(IRID,NFUND,NCELLD)
      DOUBLE PRECISION GSH(NGSHD)
      INTEGER IFUNM(LMXSPD,NAEZD)
      INTEGER IPAN(NAEZD)
      INTEGER LLMSP(NFUND,NAEZD)
      INTEGER LMSP(LMXSPD,NAEZD)
      INTEGER ILM(NGSHD,3)
      INTEGER NFU(NAEZD)
      INTEGER NTCELL(NAEZD)
      INTEGER IMAXSH(0:LMPOTD)

C     .. reference clusters
      DOUBLE PRECISION RCUTXY
      DOUBLE PRECISION RCUTZ

      INTEGER NCLS

      DOUBLE PRECISION RCLS(3,NACLSD,NCLSD)
      INTEGER ATOM(NACLSD,NAEZD)
      INTEGER CLS(NAEZD)
      INTEGER EZOA(NACLSD,NAEZD)
      INTEGER NACLS(NCLSD)
C     NUMN0(i) gives number of ref. cluster atoms around atom i
      INTEGER NUMN0(NAEZD)
C     INDN0(i,j) gives the atom index (from basis) of cluster atom j
C     around central atom i
      INTEGER INDN0(NAEZD,NACLSD)
      INTEGER REFPOT(NAEZD)

C     .. reference system ..
      DOUBLE PRECISION VREF(NAEZD)


C     .. radial mesh(es) ..
      DOUBLE PRECISION A(NAEZD)
      DOUBLE PRECISION B(NAEZD)
      DOUBLE PRECISION DRDI(IRMD,NAEZD)
      DOUBLE PRECISION R(IRMD,NAEZD)
      INTEGER IMT(NAEZD)
      INTEGER IRC(NAEZD)
      INTEGER IRCUT(0:IPAND,NAEZD)
      INTEGER IRMIN(NAEZD)
      INTEGER IRNS(NAEZD)
      INTEGER IRWS(NAEZD)

C     .. Brillouin zone ..
C     kpoints in each direction
      INTEGER INTERVX
      INTEGER INTERVY
      INTEGER INTERVZ
C     number of symmetries = number of symmetry matrices
      INTEGER NSYMAT

C     symmetry matrices
      DOUBLE COMPLEX DSYMLL(LMMAXD,LMMAXD,NSYMAXD)
      INTEGER ISYMINDEX(NSYMAXD)

C     .. Clebsch-Gordon coefficients and spherical harmonics
      INTEGER IEND

      DOUBLE PRECISION CLEB(NCLEB,2)
      DOUBLE PRECISION WG(LASSLD)
      DOUBLE PRECISION YRG(LASSLD,0:LASSLD,0:LASSLD)
      INTEGER ICLEB(NCLEB,3)
      INTEGER JEND(LMPOTD,0:LMAXD,0:LMAXD)
      INTEGER LOFLM(LM2D)

C     .. core states ..
      INTEGER ITITLE(20,NPOTD)
      INTEGER LCORE(20,NPOTD)
      INTEGER NCORE(NPOTD)

C     .. Self-consistency parameters ..
      DOUBLE PRECISION FCM
      DOUBLE PRECISION MIXING
      DOUBLE PRECISION QBOUND

      INTEGER SCFSTEPS
      INTEGER IMIX
C     has no effect, use ITDBRYD in inc.p instead
      INTEGER ITDBRY

C     .. Iterative solver options ..
      DOUBLE PRECISION QMRBOUND

      INTEGER IGUESS
      INTEGER BCP

C     .. auxillary variables, not passed to kkr2
      DOUBLE PRECISION PI
      DOUBLE PRECISION EREF
      DOUBLE PRECISION HFIELD

      INTEGER I1
      INTEGER IPE
      INTEGER IPF
      INTEGER IPFE
      INTEGER IFILE

      LOGICAL EVREF
      LOGICAL LCARTESIAN

      CHARACTER(len=40) I13
      CHARACTER(len=40) I19

C     needed for EMESHT
      DOUBLE COMPLEX DEZ(IEMXD)

      DOUBLE PRECISION RMTNEW(NAEZD)

      INTEGER INIPOL(NAEZD)

C ------------ end of declarations ---------------------------------


C===================================================================
C next short section used to search for file 'VREF'
C if present - DP-value given in that file will be used as EREF
C===================================================================
      INQUIRE(FILE='VREF',EXIST=EVREF)
      IF (EVREF) THEN
        OPEN(87,FILE='VREF',FORM='formatted')
        READ(87,*) EREF
        CLOSE(87)
      ENDIF

C     The default value for the repulsive reference potential is 8
C     This value is used if file VREF does not exist
C     (VREF should contain only one double precision value used as EREF)
C     in future: move as parameter to inputfile
      DO I1 = 1,NAEZD
        VREF(I1) = 8.D0
        IF (EVREF) VREF(I1) = EREF 
      END DO
C===================================================================
C===================================================================
      PI = 4.0D0*ATAN(1.0D0)
      EFERMI = 0.0d0


      CALL RINPUT99(BRAVAIS,ALAT,RBASIS,ABASIS,BBASIS,CBASIS,CLS,NCLS,
     &              E1,E2,TK,NPOL,NPNT1,NPNT2,NPNT3,
     &              SCFSTEPS,IMIX,MIXING,QBOUND,FCM,
     &              ITDBRY,IRNS,NTCELL,NAEZ,IRM,ZAT,
     &              NREF,ICST,IFILE,IPE,IPF,IPFE,
     &              KHFELD,KPRE,KTE,
     &              KVMAD,KVREL,KXC,LMAX,LMPOT,LPOT,
     &              NSPIN,REFPOT,
     &              ISHIFT,INTERVX,INTERVY,INTERVZ,
     &              HFIELD,VBC,VCONST,INIPOL,
     &              I13,I19,
     &              RCUTZ,RCUTXY,RCUTJIJ,JIJ,RCUTTRC,
     &              LDAU,
     &              RMTREF,KFORCE,
     &              IGUESS,BCP,QMRBOUND,LCARTESIAN,RMAX,GMAX,
     &              LMAXD, IRNSD, TRC, LPOTD, NSPIND,
     &              IRMD, NAEZD)

C     in case of a LDA+U calculation - read file 'ldauinfo'
C     and write 'wldau.unf', if it does not exist already
      IF (LDAU) THEN
        CALL ldauinfo_read(LMAXD, NSPIND, ZAT, NAEZD)
      END IF


C     IF(NAEZD.GT.100) IPE=0

C
      NSRA = 1
      IF (KVREL.GE.1) NSRA = 2
C
      CALL TESTDIM(NSPIN,NAEZ,LMAX,IRM,NREF,IRNS,
     &             LMAXD,IRMD,IRNSD,NREFD,NSPIND)
C
      OPEN (19,FILE=I19,STATUS='old',FORM='formatted')
      OPEN (IFILE,FILE=I13,STATUS='old',
     &                         FORM='formatted')
C
C
      E2IN = E2

      CALL STARTB1(IFILE,IPF,IPFE,IPE,KHFELD,
     &             1,NAEZ,
     &             RMTNEW,RMT,ITITLE,HFIELD,IMT,IRC,VCONST,
     &             IRNS,LPOT,NSPIN,IRMIN,NTCELL,IRCUT,IPAN,
     &             THETAS,IFUNM,NFU,LLMSP,LMSP,E2IN,
     &             VBC,RWS,LCORE,NCORE,DRDI,
     &             R,ZAT,A,B,IRWS,INIPOL,1,
C                  New after inc.p replace
     &             IPAND, IRID, NFUND, IRMD, NCELLD,
     &             NAEZD, IRNSD)

      CLOSE(IFILE)
      CLOSE(19)
C
C ----------------------------------------------------------------------
C update Fermi energy, adjust energy window according to running options
C
      IF ( NPOL.EQ.0 ) EFERMI = E2IN
C     a test if E2IN is different from E2 after call to STARTB1,
C     before it was =E2
      IF ( DABS(E2IN-E2).GT.1D-10 .AND. NPOL.NE.0 ) E2 = E2IN
C
C --> set up energy contour
C
      CALL EMESHT(EZ,DEZ,IELAST,E1,E2,E2IN,TK,
     &            NPOL,NPNT1,NPNT2,NPNT3,IEMXD)
      DO IE = 1,IELAST
        WEZ(IE) = -2.D0/PI*DEZ(IE)
      END DO

C
      CALL GAUNT2(WG,YRG,LMAX)
      CALL GAUNT(LMAX,LPOT,WG,YRG,CLEB,LOFLM,ICLEB,IEND,JEND,NCLEB)
C      OPEN(56,FILE='gaunt',FORM='formatted')
C      WRITE(56,FMT='(I10)') IEND
C      DO I = 1,IEND
C      WRITE(56,FMT='(3I5,1P,D25.17)')
C     +            ICLEB(I,1),ICLEB(I,2),ICLEB(I,3),CLEB(I,1)
C      END DO
C      CLOSE(56)
C
C --> setup of GAUNT coefficients C(l,m;l',m';l'',m'') for all 
C     nonvanishing (l'',m'')-components of the shape functions THETAS
C
      CALL SHAPE(LPOT,NAEZ,GSH,ILM,IMAXSH,LMSP,NTCELL,WG,YRG,LMAX,NGSHD)
C      OPEN(56,FILE='gaunt_shape',FORM='formatted')
C      WRITE(56,FMT='(I10)') IMAXSH(LMPOTD)
C      DO I = 1,IMAXSH(LMPOTD)
C      WRITE(56,FMT='(3I5,1P,D25.17)')
C     +            ILM(I,1),ILM(I,2),ILM(I,3),GSH(I)
C      END DO
C      CLOSE(56)
C
C ================================================ deal with the lattice
      CALL LATTIX99(ALAT,BRAVAIS,
     &              RECBV,VOLUME0,RR,NR,NRD)
C
      CALL SCALEVEC(RBASIS,ABASIS,BBASIS,CBASIS,
     &              NAEZ,BRAVAIS,LCARTESIAN)
C ======================================================================
C
C ======================================================================



      CALL CLSGEN99(NAEZ,RR,NR,RBASIS,CLS,NACLS,REFPOT,ATOM,
     &              EZOA,
     &              RCLS,RCUTZ,RCUTXY,
     &              NUMN0,INDN0,
     &              NRD, NCLSD, NACLSD)

cxxcpl test dimensions for Jij-calculation ..
      CALL CLSJIJ0(NAEZ,RR,NR,RBASIS,RCUTJIJ,JIJ,NRD,NXIJD)
cxxcpl .

CGC2 ===================================================================
CGC2  generate cluster 2 in order to truncate GLLKE, GLLH, etc.
CGC2 ===================================================================
C
      IF (TRC.EQ.1) THEN
        CALL CLSGEN_TRC(NAEZ,RR,NR,RBASIS,
     &                  RCUTTRC,ALAT,
     &                  NRD, NATRCD, NUTRCD)
      ENDIF
CGC2 ===================================================================
CGC2 ===================================================================
C
C
C
C ======================================================================
C     setting up kpoints
C ======================================================================
      CALL BZKINT0(NAEZ,
     &             RBASIS,BRAVAIS,RECBV,
     &             NSYMAT,ISYMINDEX,
     &             DSYMLL,
     &             INTERVX,INTERVY,INTERVZ,
     &             IELAST,EZ,KMESH,MAXMESH,MAXMSHD,
     &             LMAX, IEMXD, KREL, KPOIBZ, EKMD, IGUESSD)
C ======================================================================
C ======================================================================
C
C
C ======================================================================
C =        write out information for the other program parts           =
C ======================================================================
C
      IF (IGUESS.GT.IGUESSD) THEN
        STOP ' ERROR: to activate initial guess, set IGUESSD=1'
      ENDIF
      IF (BCP.GT.BCPD) THEN
        STOP ' ERROR: to activate bc-preconditioning, set BCPD=1'
      ENDIF
C
C     Conversion of RMAX and GMAX to units of ALAT
      RMAX = RMAX*ALAT
      GMAX = GMAX/ALAT
C
      CALL TESTDIMLAT(ALAT,BRAVAIS,RECBV,RMAX,GMAX,
     &                NMAXD, ISHLD)


      CALL WUNFILES(NPOL,NPNT1,NPNT2,NPNT3,IELAST,TK,E1,E2,EZ,WEZ,
     &              BRAVAIS,RECBV,VOLUME0,RMAX,GMAX,
     &              EFERMI,VBC,
     &              SCFSTEPS,LCORE,NCORE,
     &              NSRA,NAEZ,NREF,NSPIN,LMAX,
     &              NCLS,ICST,IPAN,IRCUT,ALAT,ZAT,R,DRDI,
     &              REFPOT,RMTREF,VREF,IEND,JEND,CLEB,ICLEB,
     &              ATOM,CLS,RCLS,NACLS,LOFLM,
     &              RBASIS,RR,EZOA,
     &              KMESH,MAXMESH,NSYMAT,
     &              DSYMLL,
     &              A,B,IFUNM,ITITLE,
     &              LMSP,NTCELL,THETAS,
     &              LPOT,LMPOT,
     &              IMIX,MIXING,QBOUND,FCM,KPRE,KTE,
     &              KVMAD,KXC,ISHIFT,KFORCE,
     &              LLMSP,IMT,IRC,IRMIN,IRNS,IRWS,RWS,RMT,NFU,
     &              GSH,ILM,IMAXSH,
     &              IEMXD,IRMD,LMPOTD,NPOTD,NAEZD,
     &              LMMAXD,IPAND,NREFD,LMAXD,
     &              NCLEB,NACLSD,NCLSD,LM2D,NRD,
     &              NSYMAXD,IRID,NFUND,
     &              NCELLD,LMXSPD,NGSHD,NUMN0,INDN0,
     &              IGUESS,BCP,QMRBOUND,
     &              NR,RCUTJIJ,JIJ,LDAU,ISYMINDEX)
C ======================================================================
C

      END

