module mod_vxcdrv

contains

SUBROUTINE VXCDRV(EXC,KTE,KXC,LPOT,NSPIN,NSTART,NEND,RHO2NS,VONS, &
                  R,DRDI,A,IRWS,IRCUT,IPAN,NTCELL,KSHAPE,GSH,ILM, &
                  IMAXSH,IFUNM,THETAS,LMSP)
use global_variables

use mod_DataTypes, only: dp
use mod_sphere_nogga
use mod_sphere_gga
use mod_vxcgga
use mod_vxclm
IMPLICIT NONE
!INCLUDE 'inc.p'
! Parameters ..
integer IJD!,LMPOTD,LMXSPD
!parameter (LMPOTD= (LPOTD+1)**2,LMXSPD= (2*LPOTD+1)**2)
parameter (IJD = 434)

! Scalar Arguments ..
integer KSHAPE,KTE,KXC,LPOT,NEND,NSPIN,NSTART

! Array Arguments ..
real (kind=dp) A(NATYPD),DRDI(IRMD,*),EXC(0:LPOTD,*),GSH(*), &
                 R(IRMD,*),RHO2NS(IRMD,LMPOTD,NATYPD,*), &
                 THETAS(IRID,NFUND,*),VONS(IRMD,LMPOTD,*)
integer IFUNM(NATYPD,*),ILM(NGSHD,3),IMAXSH(0:LMPOTD),IPAN(*), &
        IRCUT(0:IPAND,*),IRWS(*),LMSP(NATYPD,*),NTCELL(*)

! Local Arrays ..
real (kind=dp) DYLMF1(IJD,LMPOTD),DYLMF2(IJD,LMPOTD), &
                 DYLMT1(IJD,LMPOTD),DYLMT2(IJD,LMPOTD), &
                 DYLMTF(IJD,LMPOTD),RHO2IAT(IRMD,LMPOTD,2), &
                 RIJ(IJD,3),THET(IJD),WTYR(IJD,LMPOTD), &
                 YLM(IJD,LMPOTD),YR(IJD,LMPOTD)
integer IFUNMIAT(LMXSPD),LMSPIAT(LMXSPD)

! Local Scalars ..
integer IATYP,ICELL,IPOT,LMX1

IF (KXC.LT.3) THEN
  CALL SPHERE_NOGGA(LPOT,YR,WTYR,RIJ,IJD)
ELSE
  CALL SPHERE_GGA(LPOT,YR,WTYR,RIJ,IJD,LMPOTD,THET,YLM,DYLMT1, &
                  DYLMT2,DYLMF1,DYLMF2,DYLMTF)
END IF
DO IATYP = NSTART,NEND
  ICELL = NTCELL(IATYP)
  IPOT = NSPIN* (IATYP-1) + 1
  DO LMX1 = 1,LMXSPD
    IFUNMIAT(LMX1) = IFUNM(ICELL,LMX1)
    LMSPIAT(LMX1) = LMSP(ICELL,LMX1)
  END DO
  CALL DCOPY(IRMD*LMPOTD,RHO2NS(1,1,IATYP,1),1,RHO2IAT(1,1,1),1)
  IF (NSPIN.EQ.2 .OR. KREL.EQ.1) THEN
    CALL DCOPY(IRMD*LMPOTD,RHO2NS(1,1,IATYP,2),1,RHO2IAT(1,1,2),1)
  END IF
  IF (KXC.LT.3) THEN
    CALL VXCLM(EXC,KTE,KXC,LPOT,NSPIN,IATYP,RHO2IAT, &
               VONS(1,1,IPOT),R(1,IATYP),DRDI(1,IATYP), &
               IRWS(IATYP),IRCUT(0,IATYP),IPAN(IATYP), &
               KSHAPE,GSH,ILM,IMAXSH,IFUNMIAT,THETAS(1,1,ICELL), &
               YR,WTYR,IJD,LMSPIAT)
  ELSE

    ! GGA EX-COR POTENTIAL

    CALL VXCGGA(EXC,KTE,KXC,LPOT,NSPIN,IATYP,RHO2IAT, &
                VONS(1,1,IPOT),R(1,IATYP),DRDI(1,IATYP),A(IATYP), &
                IRWS(IATYP),IRCUT(0,IATYP),IPAN(IATYP), &
                KSHAPE,GSH,ILM,IMAXSH,IFUNMIAT,THETAS(1,1,ICELL), &
                WTYR,IJD,LMSPIAT,THET,YLM,DYLMT1,DYLMT2, &
                DYLMF1,DYLMF2,DYLMTF)
  END IF
END DO
END

end module mod_vxcdrv
